(define "Center" 24) // Center vertex index (= boardRings * 8)

(define "Horizontal" 1)
(define "Vertical" 2)
(define "Diagonal" 3)

(define "MoveDiagonal" (move Slide FL) (move Slide FR) (move Slide BL) (move Slide BR))
(define "MoveHorizontal" (move Slide Leftward) (move Slide Rightward))
(define "MoveVertical" (move Slide Forward) (move Slide Backward))

(define "SpiderMoveFstH" (move Slide
                               (union Leftward Rightward)
                               (then (and {(if (and (is Empty (last From)) (= (last From) "Center"))
                                             (add (piece "cross0") (to (last From)))) 
                                           (set Var "moveType" "Horizontal") 
                                           (moveAgain)}))))

(define "SpiderMoveFstV" (move Slide
                               (union Forward Backward)
                               (then (and {(set Var "moveType" "Vertical")
                                           (moveAgain)}))))

(define "SpiderMoveFstD" (or {("MoveDiagonal")}
                             (then (and {(set Var "moveType" "Diagonal")
                                         (moveAgain)}))))

(define "SpiderMoveFst" (or {("SpiderMoveFstH")
                             ("SpiderMoveFstV")
                             ("SpiderMoveFstD")}
                            (then (if (and (is Empty (last From)) (= (last From) "Center"))
                                    (add (piece "cross0") (to (last From)))))))

(define "SpiderMoveSnd" (if (= (var "moveType") "Horizontal")
                          (or {("MoveVertical") ("MoveDiagonal")})
                          (if (= (var "moveType") "Vertical")
                            (or {("MoveHorizontal") ("MoveDiagonal")})
                            (or {("MoveHorizontal") ("MoveVertical")}))
                          (then (add (piece "cross0") (to (last From))))))

(game "A Dance Of Spiders"
      (players 2)

      (equipment {(board (add (concentric Square rings:3 joinCorners:True) vertices:{{0 0}} connect:True) use:Vertex)
                  
                  (piece "spider" Each)
                  (piece "cross" Neutral)})
      
      (rules (start (place Stack items:{"spider2" "spider1"} coord:"D4"))
      
             (play (if (is Even (count Moves))
                     (forEach Piece "SpiderMoveFst")
                     (forEach Piece "SpiderMoveSnd")))

             (end (if (no Moves Mover) (result Mover Loss)))))

(metadata (graphics {(piece Rename piece:"spider" "counter_isometric")}))